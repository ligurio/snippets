css {

   source = dataurl | url | uri 
          | ("file" | "localhost" | "local" | "source" | "URL" | "URI") "(" source ")"

   # many attributes and values have vendor-specific attributes, so guess some of them
   mozweb = "" | ("-o-" | "-webkit-" | "-moz-") # can be removed when the standard ones are supported

   # http://www.w3.org/TR/css3-2d-transforms/

   num = num.natural | num.natural | num.integer | num.number

   # FIXME: no mozilla or ie gradients here, and not checked what opera uses
   procedural-image 
      = "-webkit-gradient(" ("linear" | "radial") 
         "," num.number 
         "," num.number 
         "," num.number 
         "," num.number 
         (",from(" color "),to(" color ")")*
         ")"

   ## CSS all properties (the below ones)

   generic {
      # courtesy of lynx -dump http://meiert.com/en/indices/css-properties/ | grep " yes " | sed -e 's/.*\]/"/' -e 's/ .*/" |/'  | fmt

      css-any-property = "" | "alignment-adjust" | "alignment-baseline" | "animation" | "animation-delay" | "animation-direction" | "animation-duration" | "animation-iteration-count" | "animation-name" | "animation-play-state" | "animation-timing-function" | "appearance" | "azimuth" | "backface-visibility" | "background" | "background-attachment" | "background-break" | "background-clip" | "background-color" | "background-image" | "background-origin" | "background-position" | "background-repeat" | "background-size" | "baseline-shift" | "binding" | "bleed" | "bookmark-label" | "bookmark-level" | "bookmark-state" | "bookmark-target" | "border" | "border-bottom" | "border-bottom-color" | "border-bottom-left-radius" | "border-bottom-right-radius" | "border-bottom-style" | "border-bottom-width" | "border-collapse" | "border-color" | "border-image" | "border-image-outset" | "border-image-repeat" | "border-image-slice" | "border-image-source" | "border-image-width" | "border-left" | "border-left-color" | "border-left-style" | "border-left-width" | "border-radius" | "border-right" | "border-right-color" | "border-right-style" | "border-right-width" | "border-spacing" | "border-style" | "border-top" | "border-top-color" | "border-top-left-radius" | "border-top-right-radius" | "border-top-style" | "border-top-width" | "border-width" | "bottom" | "box-align" | "box-decoration-break" | "box-direction" | "box-flex" | "box-flex-group" | "box-lines" | "box-ordinal-group" | "box-orient" | "box-pack" | "box-shadow" | "box-sizing" | "break-after" | "break-before" | "break-inside" | "caption-side" | "clear" | "clip" | "color" | "color-profile" | "column-count" | "column-fill" | "column-gap" | "column-rule" | "column-rule-color" | "column-rule-style" | "column-rule-width" | "column-span" | "column-width" | "columns" | "content" | "counter-increment" | "counter-reset" | "crop" | "cue" | "cue-after" | "cue-before" | "cursor" | "direction" | "display" | "dominant-baseline" | "drop-initial-after-adjust" | "drop-initial-after-align" | "drop-initial-before-adjust" | "drop-initial-before-align" | "drop-initial-size" | "drop-initial-value" | "elevation" | "empty-cells" | "fit" | "fit-position" | "float" | "float-offset" | "font" | "font-family" | "font-size" | "font-size-adjust" | "font-stretch" | "font-style" | "font-variant" | "font-weight" | "grid-columns" | "grid-rows" | "hanging-punctuation" | "height" | "hyphenate-after" | "hyphenate-before" | "hyphenate-character" | "hyphenate-lines" | "hyphenate-resource" | "hyphens" | "icon" | "image-orientation" | "image-resolution" | "inline-box-align" | "left" | "letter-spacing" | "line-height" | "line-stacking" | "line-stacking-ruby" | "line-stacking-shift" | "line-stacking-strategy" | "list-style" | "list-style-image" | "list-style-position" | "list-style-type" | "margin" | "margin-bottom" | "margin-left" | "margin-right" | "margin-top" | "mark" | "mark-after" | "mark-before" | "marks" | "marquee-direction" | "marquee-play-count" | "marquee-speed" | "marquee-style" | "max-height" | "max-width" | "min-height" | "min-width" | "move-to" | "nav-down" | "nav-index" | "nav-left" | "nav-right" | "nav-up" | "opacity" | "orphans" | "outline" | "outline-color" | "outline-offset" | "outline-style" | "outline-width" | "overflow" | "overflow-style" | "overflow-x" | "overflow-y" | "padding" | "padding-bottom" | "padding-left" | "padding-right" | "padding-top" | "page" | "page-break-after" | "page-break-before" | "page-break-inside" | "page-policy" | "pause" | "pause-after" | "pause-before" | "perspective" | "perspective-origin" | "phonemes" | "pitch" | "pitch-range" | "play-during" | "position" | "presentation-level" | "punctuation-trim" | "quotes" | "rendering-intent" | "resize" | "rest" | "rest-after" | "rest-before" | "richness" | "right" | "rotation" | "rotation-point" | "ruby-align" | "ruby-overhang" | "ruby-position" | "ruby-span" | "size" | "speak" | "speak-header" | "speak-numeral" | "speak-punctuation" | "speech-rate" | "stress" | "string-set" | "table-layout" | "target" | "target-name" | "target-new" | "target-position" | "text-align" | "text-align-last" | "text-decoration" | "text-emphasis" | "text-height" | "text-indent" | "text-justify" | "text-outline" | "text-shadow" | "text-transform" | "text-wrap" | "top" | "transform" | "transform-origin" | "transform-style" | "transition" | "transition-delay" | "transition-duration" | "transition-property" | "transition-timing-function" | "unicode-bidi" | "vertical-align" | "visibility" | "voice-balance" | "voice-duration" | "voice-family" | "voice-pitch" | "voice-pitch-range" | "voice-rate" | "voice-stress" | "voice-volume" | "volume" | "white-space" | "white-space-collapse" | "widows" | "width" | "word-break" | "word-spacing" | "word-wrap" | "z-index" 

      css-generic-value = num.number 
                        | "\"" str.string "\"" 
                        | ("auto" | "none" | "disabled" | "yes" | "no" | "any")
                        | css-generic-value (" " css-generic-value)*
                        | source
                        | css-generic-value " !important"
  
      exp = css-any-property  ":" css-generic-value
   }

   ## CSS WEBKIT 

   webkit {
      assignment
         = "appearence:" ( "sliderthumb-horizontal" | "checkbox" | "radio" | "menulist" | "meter" | "progress-bar" | "datagrid" 
                         | "media-mute-button" | "media-play-button" | "media-timeline-container" | "media-current-time-display") 
         | "box-shadow:" (num.number "px "){3} color
         | "border-radius:" num.number "px"
         | "transition:" "opacity " num.number "s ease" # <- fixme
         | "box-flex: 1" # fixme
         | "background:gradient(" ("linear" | "radial")
            "," num.number
            "," num.number
            "," num.number
            "," num.number
            ",from(" color "),to(" color ")"
            ("" | (",color-stop(" num.number "," color ")"){3})
            ")"

                 
      exp = "-webkit-" assignment
   }

   ## CSS MISC (sort later)

   unsorted {

      ## partial things
      alignment = "middle"
      color = "#" num.hex{3}
            | "#" num.hex{6}
            | ("currentcolor" | mozweb "activelink" | mozweb "focus-ring-color" | mozweb "link" | mozweb "text")
            | ("activeborder" | "activecaption" | "appworkspace" | "aqua" | "background" | "black" | "blue" | "buttonface" | "buttonhighlight" | "buttonshadow" | "buttontext" | "captiontext" | "fuchsia" | "gray" | "graytext" | "green" | "grey" | "highlight" | "highlighttext" | "inactiveborder" | "inactivecaption" | "inactivecaptiontext" | "infobackground" | "infotext" | "lime" | "maroon" | "match" | "menu" | "menutext" | "navy" | "olive" | "orange" | "purple" | "red" | "scrollbar" | "silver" | "teal" | "threeddarkshadow" | "threedface" | "threedhighlight" | "threedlightshadow" | "threedshadow" | "transparent" | "white" | "window" | "windowframe" | "windowtext" | "yellow")
      u-range = "U+" num.natural "-" num.natural  # unicode range 

      css-val = num.number | "\"" str.string "\"" | color | css-val " " css-val | source 

      font-name = "Helvetica" | "any" | "serif" | "sans-serif"

      pixs = num.integer "px"

      font-list = font-name ("," font-name)*

      at-font-face
         = "@font-face {" 
            ("" | "font-family:" font-list ";")
            ("" | "font-style:" ("italic" | "normal" | "oblique") ";")
            ("" | "src:" source ";")
         "}"

      ##########
      
      media-name = "all" | "print"  | "screen" | "speech"
      media-transform = "transform-2d" | "transform-3d" | "transition" | "animation"

      # todo: check if it is safe to mozweb the below things

      # apply only to given type of media (or when X is available)
      exp = "@media " (media-name | media-transform) "{" exps "}"
          | "cursor: " (mozweb "grab" | mozweb "grabbing" | mozweb "zoom-in" | mozweb "zoom-out" | "alias" | "all-scroll" | "auto" | "cell" | "col-resize" | "context-menu" | "copy" | "crosshair" | "default" | "e-resize" | "ew-resize" | "hand" | "help" | "move" | "n-resize" | "ne-resize" | "nesw-resize" | "no-drop" | "none" | "not-allowed" | "ns-resize" | "nw-resize" | "nwse-resize" | "pointer" | "progress" | "row-resize" | "s-resize" | "se-resize" | "sw-resize" | "text" | "vertical-text" | "w-resize" | "wait")
          | "outline: " (color | mozweb "focus-ring-color" | "invert") # color
            " auto" # style
            " " num.natural # width
          | "outline-offset:" num.integer 
          | "pointer-events: " ("auto" | "none")
          | mozweb "box-align:" (alignment | "baseline" | "center" | "end" | "start" | "stretch")
          | mozweb "box-direction:" ("normal" | "reverse")
          | mozweb "box-flex: " num.number # really float
          | mozweb "box-flex-group:" num.natural
          | mozweb "box-lines:" ("multiple" | "single")
          | mozweb "box-ordinal-group:" num.natural
          | mozweb "box-orient:" ("block-axis" | "horizontal" | "inline-axis" | "vertical")
          | mozweb "box-pack:" ("center" | "end" | "justify" | "start")
          | mozweb "touch-callout:" ("inherit" | "none")
          | mozweb "dashboard-region:" css-val
          | mozweb "tap-highlight-color: " color
          | mozweb "" ("border-fit" | "font-size-delta" | "highlight" | "line-clamp" | "match-nearest-mail-blockquote-color" | "text-decorations-in-effect" | "transition-repeat-count") ":" css-val
          | "letter-spacing:" num.number
          | "text-decoration:" ("line-through" | "none" | "overline" | "underline")
          | "text-indent:" (num.number | num.natural)
          | "text-overflow:" ("clip" | "ellipsis")
          | "text-shadow:" color " " num.number " " num.number " " num.number
          | "text-transform:" ("capitalize" | "lowercase" | "none" | "uppercase")
          | "white-space:" ("normal" | "nowrap" | "pre" | "pre-line" | "pre-wrap")
          | "word-break:" ("break-all" | "break-word" | "normal")
          | "word-spacing:" num.number
          | "word-wrap:" ("break-word" | "normal")
          | mozweb "marquee:" 
               ("ahead" | "auto" | "backwards" | "down" | "forwards" | "left" | "reverse" | "right" | "up") # direction
               " " num.number " " num.number " " 
               ("alternate" | "none" | "scroll" | "slide") # marquee style
               " " num.number
          | mozweb "text-fill-color:"  color
          | mozweb "text-security:" ("circle" | "disc" | "none " | "square") 
          | mozweb "text-size-adjust:" num.number
          | mozweb "text-stroke:" num.number " " color
          | mozweb "line-break:" ("after-white-space" | "normal")
          | mozweb "appearance: " ("caps-lock-indicator" | "button" | "button-bevel" | "caret" | "checkbox" | "default-button" | "listbox" | "listitem" | "media-fullscreen-button" | "media-mute-button" | "media-play-button" | "media-seek-back-button" | "media-seek-forward-button" | "media-slider" | "media-sliderthumb" | "menulist" | "menulist-button" | "menulist-text" | "menulist-textfield" | "none" | "push-button" | "radio" | "searchfield" | "searchfield-cancel-button" | "searchfield-decoration" | "searchfield-results-button" | "searchfield-results-decoration" | "slider-horizontal" | "slider-vertical" | "sliderthumb-horizontal" | "sliderthumb-vertical" | "square-button" | "textarea" | "textfieldscrollbarbutton-down" | "scrollbarbutton-left" | "scrollbarbutton-right" | "scrollbarbutton-up" | "scrollbargripper-horizontal" | "scrollbargripper-vertical" | "scrollbarthumb-horizontal" | "scrollbarthumb-vertical" | "scrollbartrack-horizontal" | "scrollbartrack-vertical")
          | mozweb "nbsp-mode: " ("normal" | "space")
          | mozweb "rtl-ordering:" ("logical" | "visual")
          | mozweb "user-drag:" ("element" | ("auto" | "none"))
          | mozweb "user-modify:" ("read-only" | "read-write" | "read-write-plaintext-only")
          | mozweb "user-select:" ("auto" | "none" | "text")
          | mozweb "margin-before:" num.number
          | mozweb "margin-after:" num.number
          | mozweb "margin-start:" num.number
          | mozweb "margin-end:" num.number
          | mozweb "padding-start:" num.number
          | mozweb "padding-end:" num.number
          | "border-collapse:" ("collapse" | "separate")
          | "border-spacing:" num.number
          | "caption-side:" ("bottom" | "left" | "right" | "top")
          | "empty-cells:" ("hide" | "show")
          | "font-family: " font-name ("," font-name)*
          | "font-size:" (num.number | num.natural | mozweb "xxx-large")
          | "font-style:" ("italic" | "normal" | "oblique")
          | "font-variant:" ("normal" | "small-caps")
          | "font-weight:" num.number
          | "src:" source ("," source)*
          | "unicode-range:" u-range ("," u-range)*
          | "text-align:" (mozweb "" ("auto" | "center" | "left" | "right") | ("center" | "end" | "justify" | "left" | "right" | "start"))
          | "background-color:" color
          | "background-image:" procedural-image
          | "background-position:" num.integer ("" | " " num.integer)
          | "color:" color
          | mozweb "background-clip:" ("border" | "content" | "padding" | "text")
          | mozweb "background-composite:" ("padding" | "padding" | "border")
          | mozweb "background-size:" num.number ("" | " " num.number)
          | "orphans:" num.integer
          | "widows:" num.integer
          | "page-break-after:" ("always" | "auto" | "avoid" | "left" | "right")
          | "page-break-before:" ("always" | "auto" | "avoid" | "left" | "right")
          | "page-break-inside:" ("auto" | "avoid")
          | "counter-increment: section " num.number
          | "counter-reset: section"
          | "content:" css-val
          | "bottom:" num.number
          | "clear:" ("both" | "left" | "none" | "right")
          | "direction:" ("ltr" | "rtl")
          | "display:" (mozweb "box" | mozweb "inline-box" | "block" | "compact" | "inline" | "inline-block" | "inline-table" | "list-item" | "none" | "run-in" | "table" | "table-caption" | "table-cell" | "table-column" | "table-column-group" | "table-footer-group" | "table-header-group" | "table-row" | "table-row-group")
          | "float:" ("center" | "left" | "none" | "right")
          | "height:" (num.number | "auto" | "intrinsic" | "min-intrinsic")
          | "left:" num.number
          | "line-height:" num.number
          | "max-height:" (num.number | "none" | "min-intrinsic" | "intrinsic")
          | "max-width:" (num.number | "none" | "min-intrinsic" | "intrinsic")
          | "min-height:" (num.number | "min-intrinsic" | "intrinsic")
          | "min-width:" (num.number | "min-intrinsic" | "intrinsic")
          | "opacity:" num.number
          | "position:" ("absolute" | "fixed" | "relative" | "static")
          | "right:" num.number
          | "top:" num.number
          | "width:" num.number
          | "z-index:" num.number
          | "zoom:" num.number
          | "unicode-bidi:" ("bidi-override" | "embed" | "normal")
          | "vertical-align:" (mozweb "baseline-middle" | "baseline" | "bottom" | "middle" | "sub" | "super" | "text-bottom" | "text-top" | "top")
          | "overflow:" (mozweb "marquee" | "auto" | "hidden" | "overlay" | "scroll" | "visible")
          | "overflow-x:" (mozweb "marquee" | "auto" | "hidden" | "overlay" | "scroll" | "visible")
          | "overflow-y:" (mozweb "marquee" | "auto" | "hidden" | "overlay" | "scroll" | "visible")
          | "visibility: " ("collapse" | ("hidden" | "visible"))
          | "clip: rect(" pixs " " pixs " " pixs " " pixs ")"

      exps = exp (";" exp)*
   }


   ## CSS Text Level 3 (http://www.w3.org/TR/2011/WD-css3-text-20110215/)

   spacing = num.number | num.number "%" 

   text-decoration = "bold" # fixme

   text3 {
      opt = "hanging-punctuation:" ("" | "first") ("" | " last") ("" |" allow-end" | " force-end")
          | "hyphenate-character:" str.string # ?
          | "hyphenate-limit-after:" num.integer
          | "hyphenate-limit-before:" num.integer
          | "hyphenate-limit-last:" ("none" | "always" | "column" | "page" | "spread")
          | "hyphenate-limit-lines:" num.integer
          | "hyphenate-resource:" source ("" | (" " source)*)
          | "hyphens:" ("none" | "manual" | "auto" | "all")
          | "letter-spacing:" spacing (" " spacing){0,2}
          | "line-break:" ("auto" | "loose" | "normal" | "strict")
          | "text-align:" ("start" | "end" | "left" | "right" | "center" | "justify" | "match-parent") ("" | str.string)
          | "text-align-first:" ("auto" | "start")
          | "text-align-last:" ("start" | "end" | "left" | "right" | "center" | "justify")
          | "text-autospace:" ("" | "ideographic-numeric") ("" | "ideographic-alpha") ("" | "punctuation")
          | "text-decoration:" (" " text-decoration)* ("" | " blink")
          | "text-decoration-color:" color
          | "text-decoration-line:" ("" | "underline") ("" | "overline") ("" | "line-through")
          | "text-decoration-skip:" ("" | "objects") ("" | "spaces") ("" | "ink") ("" | "all")
          | "text-decoration-style:" ("solid" | "dotted" | "double" | "dashed" | "wave")
          # missing text-emphasis
          | "text-emphasis-position:" ("above right" | "below right")
          | "text-emphasis-style:" ("" | "filled" | "open") " " 
               ("" | "dot" | "circle" | "double-circle" | "triangle" | "sesame")
          | "text-indent:" spacing
          | "text-justify:" ("auto" | "none" | "inter-word" | "inter-ideograph" | "inter-cluster" | "distribute" | "kashida")
      exp = opt (";" opt)* # often add many of same kind to each element
   }


   ## CSS 2D transforms


   transform-2d {

      exp = mozweb "transform: " transformation ("" | (" " transformation)*)
        ("" | ";transform-origin: " num " " num)
         # fixme: origin really [ [ <percentage> | <length> | left | center | right ] [ <percentage> | <length> | top | center | bottom ]? ] | [ [ left | center | right ] || [ top | center | bottom ] ]  

      transformation 
         = "matrix(" num ("," num){5} ")"
         | "translate(" tval ("" | "," tval) ")"
         | "translateX(" tval ")"
         | "translateY(" tval ")"
         | "scale(" num ("" | "," num)")"
         | "scaleX(" num ")"
         | "scaleY(" num ")"
         | "rotate(" angle ")"
         | "skewX(" angle ")"
         | "skewY(" angle ")"
         | "skew(" angle ("" | "," angle) ")"
      angle = num "deg"
      tval = num "px"
   }

   # http://www.w3.org/TR/2009/WD-css3-3d-transforms-20090320/
   transform-3d {
      mozweb = "" | "-webkit-" | "-moz-"

      exp = mozweb "transform: " 
            "perspective(" num ") "
            transformation (" " transformation)*
            ("" | ";transform-style:" ("flat" | "preserve-3d"))   
            ("" | ";backface-visibility:" ("visible" | "hidden"))

      transformation 
         = "perspective: " num 
         | "matrix(" num ("," num){5} ")"               # 2d 2x3 matrix
         | "matrix3d(" num ("," num){15} ")"            # 3d 4x4 matrix
         | "translate(" tval ("" | ("," tval)) ")"
         | "translate3d(" tval ("," tval){2} ")"        # 3d translation vector
         | "translateX(" tval ")"
         | "translateY(" tval ")"
         | "translateZ(" tval ")"
         | "scale(" num ("" | "," num)")"
         | "scaleX(" num ")"
         | "scaleY(" num ")"
         | "scaleZ(" num ")"
         | "rotate(" angle ")"
         | "rotate3d(" num "," num "," num "," angle ")"
         | "rotateX(" angle ")"
         | "rotateY(" angle ")"
         | "rotateZ(" angle ")"
         | "skewX(" angle ")"
         | "skewY(" angle ")"
         | "skew(" angle ("" | ("," angle)) ")"
      angle = num "deg"
      tval = num "px"
   }
  
   # http://www.w3.org/TR/2004/WD-css3-speech-20041216/
   speech {
      num = num.natural | num.number
      option 
         = "voice-balance:" (num | "left" | "right" | "leftwards" | "rightwards" | "inherit")
         | "speak:" ("none" | "normal" | "spell-out" | "digits" | "literal-punctuation" | "no-punctuation" | "inherit")
         | "pause-" ("before" | "after") ":" (num | "none" | "x-weak" | "weak" | "medium" | "strong" | "x-strong" | "inherit")
         | "pause:" ("pause-before" | "pause-after")
         | "rest-" ("before" | "after") ":" (num | "none" | "x-weak" | "weak" | "medium" | "strong" | "x-strong" | "inherit")
         | "rest:" ("rest-before" | "rest-after")
         | "mark-" ("before" | "after") ":" str.string
         | "voice-rate:" num "%"
         | "voice-duration: " num "s"
         | "phonemes: " str.string
      exp = option (";" option)*
   }

   color = "#" num.hex{3} | "#" num.hex{6}

   background-image 
         = mozweb "gradient(" ("linear" | "radial")
            "," num.number
            "," num.number
            "," num.number
            "," num.number
            (",from(" color "),to(" color ")")*
            ")"

   # fixme: where are most of the css things??? 

   ########################################################33
   # CSS Multi-column Layout Module
   # http://www.w3.org/TR/css3-multicol/

   css-multicol {
      break-option 
         = "auto" | "always" | "avoid" | "left" | "right" 
         | "page" | "column" | "avoid-page" | "avoid-column"

      option 
       = "break-after:" break-option
       | "break-before:" break-option
       | "break-inside:" break-option
       | "column-count:" num.number
       | "column-fill:" ("auto" | "balance")
       | "column-gap:" num.number ("" | "px" | "%")
       | "column-rule:" "que" # todo
       | "column-rule-color:" color
       | "column-rule-style:" "que" # todo
       | "column-rule-width:" num.number ("" | "px" | "%")
       | "columns:" (" " num.number " " num.number)+
       | "column-span:" ("none" | "all")
       | "column-with:" (num.number ("" | "px" | "%") | "auto")

      exp = option (";" option)*
   }

   ########################################################33
   # CSS Marquee Module Level 3
   # http://www.w3.org/TR/css3-marquee/

   css-marquee {
      option 
       = "marquee-direction:" ("forward" | "reverse")
       | "marquee-play-count:" (num.number | "infinite")
       | "marquee-speed:" ("slow" | "normal" | "fast")
       | "marquee-style:" ("scroll" | "slide" | "alternate")
       | "overflow-style:" ("auto" | "marquee-line" | "marquee-block")
      exp = option (";" option)*
   }

   ########################################################33
   # CSS Generated Content for Paged Media Module
   # http://www.w3.org/TR/2010/WD-css3-gcpm-20100608/

   css-generated {
      unit = num.number ("" | "pt" | "%")
      option 
         = "bleed:" unit
         | "bookmark-label:" "a"+
         | "bookmark-level:" num.number
         | "bookmark-state:" ("open" | "closed")
         | "bookmark-target:" ("none" | uri | "a"+)
         | "float-offset:" unit " " unit
         | "hyphenate-after:" num.number
         | "hyphenate-before:" num.number
         | "hyphenate-character:\"" ("a"+ | str.string) "\""
         | "hyphenate-lines:" num.number
         | "hyphenate-resource:" uri ("," uri)*
         | "hyphens:" ("none" | "manual" | "auto")
         | "image-resolution:" num.number "dpi"
         | "marks:" ("cross" | "crop" | "none")
         # | "string-set:" <- not here

      exp = option (";" option)*
   }

   ########################################################33
   # CSS Writing Modes Module Level 3
   # http://www.w3.org/TR/2011/WD-css3-writing-modes-20110531/

   css-writing-modes {
      option 
       = "direction:" ("ltr" | "rtl")
       | "caption-side:" ("before" | "after")
       | "column-width:" ("min-content" | "max-content" | "fill-available" | "fit-content")
       | "column-width:"
       | "text-combine:" ("none" | "horizontal" | num.number)
       | "text-orientation:" ("vertical-right" | "upright" | "rotate-right" | "rotate-left" | "rotate-normal" | "auto")
       | "unicode-bidi:" ("normal" | "embed" | "isolate" | "plaintext" | "bidi-override")
       | "writing-mode:" ("horizontal-tb" | "vertical-rl" | "vertical-lr")

      exp = option (";" option)*
   }
   
   ########################################################33
   # Flexible Box Layout Module
   # http://www.w3.org/TR/2011/WD-css3-flexbox-20110322/

   css-flexbox {
      option 
       = "flex-align:" ("auto" | "baseline")
       | "flex-direction:" ("lr" | "rl" | "tb" | "bt" | "inline" | "inline-reverse" | "block" | "block-reverse")
       | "flex-order:" num.number
       | "flex-pack:" ("start" | "end" | "center" | "justify")

      exp = "display:" ("flexbox" | "inline-flexbox") (";" option)+
   }

   exp = "color: " color
       | "background:" color
       | "background-image:" (color | background-image)
       | "opacity:" num.number
       | transform-2d.exp
       | transform-3d.exp
       | css-multicol.exp
       | css-marquee.exp
       | css-generated.exp
       | css-writing-modes.exp
       | css-flexbox.exp
       | webkit.exp
       | speech.exp
       | text3.exp
       | unsorted.exp
       | generic.exp    # any-valid-property: something-like-a-value

   exps = exp (";" exp)*
}
